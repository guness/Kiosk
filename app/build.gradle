apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    signingConfigs {
        config {
            keyPassword 'android'
            storeFile file('src/main/resources/platform.jks')
            storePassword 'android'
            keyAlias 'android'
        }
    }
    defaultConfig {
        applicationId "com.guness.kiosk"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 2
        versionName "1.0"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        jackOptions {
            enabled true
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    productFlavors {
        ACR122U {
            // These values are also in xml/device_filter file
            buildConfigField 'int', 'READER_VENDOR_ID', '1839'
            buildConfigField 'int', 'READER_PRODUCT_ID', '8704'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

ext {
    supportLibVersion = '25.0.0'
    espressoVersion = '2.2.2'
    testRunnerVersion = '0.5'
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile "com.android.support.test:runner:${testRunnerVersion}"
    androidTestCompile "com.android.support.test:rules:${testRunnerVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:${espressoVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:${espressoVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-idling-resource:${espressoVersion}"
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'

    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        androidTestCompileDependency.exclude group: 'com.android.support'
    }

    // Support library
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:support-v13:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:support-annotations:${supportLibVersion}"
    compile "com.android.support:percent:${supportLibVersion}"
    compile "com.android.support:support-vector-drawable:${supportLibVersion}"

    // Butterknife
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    // CircleImageView
    compile 'de.hdodenhof:circleimageview:2.1.0'

    // Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // Firebase
    compile 'com.google.firebase:firebase-database:9.4.0'

    compile 'com.android.support:multidex:1.0.1'

    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta1'

    compile 'eu.chainfire:libsuperuser:1.0.0.+'
}

apply plugin: 'com.google.gms.google-services'